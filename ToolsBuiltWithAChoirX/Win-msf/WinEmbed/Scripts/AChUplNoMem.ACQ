*******************************************************
* AChUpldNoMem - Flexible Upload Script - No Memory   *
*  Version 2 - Uses internal Zip to improve collection*
* - /VR0 = "S3" or "SFTP"                             *
* - /VR1 = S3 Bucket or SFTP Server                   *
* - /VR2 = S3 Region                                  *
* - /USR = S3 AWS ID or SFTP UserID                   *
* - /PWD = S3 AWS Key or SFTP Password                *
*******************************************************
SET:CopyPath=Part
SAY:
SAY: AChoir will pull artifacts and telemetry of forensic value for investigation.
SAY:
SAY: * * *   Please MAKE SURE you are running this as an Administrator  * * * 
SAY: * * * Administrator right are required for many of these utilities * * * 
SAY:
CN0:0
*******************************************************
* Should we Upload To S3                              *
*******************************************************
EQU:&VR0. S3.
 SAY:[+] Upload Type: S3
 CN0:++
END:
*******************************************************
* Should we Upload To SFTP                            *
*******************************************************
EQU:&VR0. SFTP.
 SAY:[+] Upload Type: SFTP
 CN0:++
END:
*******************************************************
* No Upload Server Information specified. Error Out   *
*******************************************************
N==:&CN0 0
 SAY:[!] Upload Type Not Specified (/VR0:S3 or /VR0:SFTP) ... Exiting.
 BYE:
END:
*******************************************************
* S3 Upload has been Specified                        *
*******************************************************
CN0:0
EQU:&VR0 S3
 EQU:&VR1. .
  SAY:[!] S3 Bucket Not Specified (/VR1:)
  CN0:++
 END:
 EQU:&Usr none
  SAY:[!] S3 AWS ID Not Specified (/USR:)
  CN0:++
 END:
 EQU:&Pwd none
  SAY:[!] S3 Secret Key Not Specified (/PWD:)
  CN0:++
 END:
 EQU:&vr2. .
  SAY:[!] S3 Region Not Specified (/VR2:)
  CN0:++
 END:
 N>>:&CN0 0
  SAY:[!] Not Enough Information for S3 Upload:
  SAY:[!] S3 Upload requires /VR1:S3Bucket /USR:S3AWSId /PWD:AWSSecretKey /VR2:AWSRegion
  SAY:[!] S3 Upload - Not Enough Information... Exiting...
  BYE:
 END:
 *******************************************************
 * Start the S3 Session                                *
 *******************************************************
 Set:S3Region=&vr2
 Set:S3Bucket=&VR1
 Set:S3AWSId=&Usr
 Set:S3AWSKey=&Pwd
 SAY:[+] S3 Upload invoked - Region:&vr2, Bucket:&VR1, AWS User:&Usr, AWS Secret Key: <Redacted>
 S3S:&Usr &Pwd
 RC!:0
  SAY:[!] Could Not Connect to S3 Bucket. Exiting...
  BYE:
 END:
END:
*******************************************************
* SFTP Upload has been Specified                      *
*******************************************************
EQU:&VR0 SFTP
 EQU:&VR1. .
  SAY:[!] SFTP Server Not Specified (/VR1:)
  CN0:++
 END:
 EQU:&Usr none
  SAY:[!] SFTP UserID Not Specified (/USR:)
  CN0:++
 END:
 EQU:&Pwd none
  SAY:[!] SFTP Password Not Specified (/PWD:)
  CN0:++
 END:
 N>>:&CN0 0
  SAY:[!] Not Enough Information for SFTP Upload:
  SAY:[!] SFTP Upload requires /VR1:SFTPServer /USR:SFTPUserId /PWD:SFTPPassword
  SAY:[!] SFTP Upload - Not Enough Informaiton... Exiting...
  BYE:
 END:
 *******************************************************
 * Start the SFTP Session                              *
 *******************************************************
 SET:SFTPServ=&VR1
 SAY:[+] SFTP Upload invoked - Server:&VR1, UserID:&Usr, SFTP PassWord: <Redacted>
 SFS:&Usr &Pwd
 RC!:0
  SAY:[!] Could Not Connect to SFTP Server. Exiting...
  BYE:
 END:
END:
*PZZ: Press Any Key to Continue, or q to QUIT
SAY:
SAY: 1. Bypassing Memory Dump...
SAY:
SAY:  WARNING: This will take while, and will create a HUGE memory image File.
SAY:  You've been warned ;-) 
SAY:
VER:Windows
 SAY: 2. Extracting and Zipping Prefetch Directory...
 ACQ:\Prf
 SET:ZipFileName=&Acq\Prf.zip
 SET:ZipFileRoot=Prf
 FOR:&Win\prefetch\**\*
 ZIP:&For
 SET:ZipFileName=close
 EQU:&VR0 S3
  SAY:
  SAY: 2a. Uploading Prefetch to S3...
  S3U:&Acq\Prf.zip \&Acn\Prf
 END:
 EQU:&VR0 SFTP
  SAY:
  SAY: 2a. Uploading Prefetch to SFTP...
  SFU:&Acq\Prf.zip \&Acn\Prf
 END:
 CLN:&Acn\Prf
 SAY:
 SAY: 3a. Copy Raw $MFT(s)...
 SAY:
 DSK:Fixed
 ACQ:\RawData
 EXE:\TSK\fcat.exe -f ntfs $MFT \\.\&dsk: --exestdout=&acn\RawData\MFT-&Dsk
 SAY:
 SAY: 3b. Copy Raw $LogFile...
 SAY:
 EXE:\TSK\fcat.exe -f ntfs $LogFile \\.\&dsk: --exestdout=&acn\RawData\LogFile-&Dsk
 SAY:
 SAY: 3c. Now Extracting USNJrnl...
 SAY:
 EXE:\DSK\ExtractUSNJrnl.exe /DevicePath:c: /OutputPath:&Acq
 SAY:
 SAY: 3d. Now Parsing USNJrnl...
 SAY:
 SYS:fsutil usn enumdata 1 0 1 C: --exestdout=&acn\RawData\USNJrnl.dat
 SYS:fsutil usn readjournal C: csv --exestdout=&acn\RawData\USNJrnl.csv
 SAY:
 EQU:&VR0 S3
  SAY: 3e. Uploading RawData to S3...
  SAY:
  S3U:&Acq\* \&Acn\RawData
 END:
 EQU:&VR0 SFTP
  SAY: 3e. Uploading RawData to SFTP...
  SAY:
  SFU:&Acq\* \&Acn\RawData
 END:
 SAY: 3f. Deleting Local Copy...
 CLN:&Acn\RawData
 SAY:
 SAY: 4a. Gathering System Information...
 SAY:
 ACQ:\
 EXE:\SYS\PSInfo.exe /accepteula -s --exestdout=&acn\Info.dat
 SAY:
 SAY: 4b. Gathering System Audit Information...
 SAY:
 EXE:\SYS\WinAudit.exe /r=gsoPxuTUeERNtnzDaIbMpmidcSArCOHG /f=&Acq\WinAudit.htm /l=&Acq\WinAudLog.txt
 SAY:
 SAY: 4c. Gathering Group Policy Information...
 SAY:
 SYS:GPResult /R /Z --exestdout=&acn\GPResult.txt
 SAY: 4d. Uploading to S3...
 SAY:
 EQU:&VR0 S3
   S3U:&Acq\Info.dat \&Acn
   S3U:&Acq\WinAud* \&Acn
   S3U:&Acq\GPResult.txt \&Acn
 END:
 EQU:&VR0 SFTP
   SFU:&Acq\Info.dat \&Acn
   SFU:&Acq\WinAud* \&Acn
   SFU:&Acq\GPResult.txt \&Acn
 END:
 SAY: 4e. Deleting Local Copy...
 DEL:&Acn\Info.dat
 DEL:&Acn\WinAud*
 DEL:&Acn\GPResult.txt
 SAY:
 SAY: 5. Raw Copy Registry Hives...
 SAY:
 ACQ:\Reg
 EXE:\TSK\fcat.exe /Windows/System32/Config/SECURITY \\.\${SYSTEMDRIVE} --exestdout=&acn\Reg\SECURITY
 EXE:\TSK\fcat.exe /Windows/System32/Config/SOFTWARE \\.\${SYSTEMDRIVE} --exestdout=&acn\Reg\SOFTWARE
 EXE:\TSK\fcat.exe /Windows/System32/Config/SAM \\.\${SYSTEMDRIVE} --exestdout=&acn\Reg\SAM
 EXE:\TSK\fcat.exe /Windows/System32/Config/SYSTEM \\.\${SYSTEMDRIVE} --exestdout=&acn\Reg\SYSTEM
 EXE:\TSK\fcat.exe /Windows/AppCompat/Programs/Amcache.hve \\.\${SYSTEMDRIVE} --exestdout=&acn\Reg\Amcache.hve
 SAY:
 EQU:&VR0 S3
  SAY: 5a. Uploading Registry to S3...
  S3U:&Acq\* \&Acn\Reg
 END:
 EQU:&VR0 SFTP
  SAY: 5a. Uploading Registry to SFTP...
  SFU:&Acq\* \&Acn\Reg
 END:
 SAY: 5b. Deleting Local Copy...
 CLN:&Acn\Reg
 SAY:
 SAY: 6. Copying/Zipping (System32) EventViewer Logs...
 SAY:
 ACQ:\Evt
 SET:ZipFileName=&Acq\Evt.zip
 SET:ZipFileRoot=Evt\Sys32
 FOR:&Win\System32\winevt\Logs\**\*
 ZIP:&For
 SET:ZipFileRoot=Evt\Nativ
 FOR:&Win\sysnative\winevt\Logs\**\*
 ZIP:&For
 SET:ZipFileName=close
 EQU:&VR0 S3
  SAY:
  SAY: 6a. Uploading Event Logs to S3...
  S3U:&Acq\Evt.zip \&Acn\Evt
 END:
 EQU:&VR0 SFTP
  SAY:
  SAY: 6a. Uploading Event Logs to SFTP...
  SFU:&Acq\Evt.zip \&Acn\Evt
 END:
 SAY: 6b. Deleting Local Copy...
 CLN:&Acn\Evt
 SAY:
 SAY: 7. Parsing (System32) Etc Directory, and SRUM...
 SAY:
 ACQ:\SYS
 ACQ:\SYS\Sys32
 CPY:"&Win\System32\Drivers\Etc\*" "&Acq"
 CPY:"&Win\System32\sru\SRUDB.dat" "&Acq"
 SAY:
 SAY: 7a. Parsing (Sysnative) Hosts And Services Directory...
 SAY:
 ACQ:\SYS\Nativ
 CPY:"&Win\sysnative\Drivers\Etc\*" "&Acq"
 CPY:"&Win\sysnative\sru\SRUDB.dat" "&Acq"
 SAY:
 SAY: 8. Parsing Environment Variables...
 SAY:
 ACQ:\SYS
 SYS:CMD /c Set --exestdout=&acn\SYS\EnVar.dat
 SAY:
 SAY: 9. Parsing The Patch List...
 SAY:
 SYS:WMIC qfe list --exestdout=&acn\SYS\QFEList.dat
 SAY:
 SAY: 10. Gathering Running Process List Information...
 SAY:
 SYS:Tasklist /v --exestdout=&acn\SYS\Tasklist.dat
 SYS:Tasklist /M --exestdout=&acn\SYS\TaskAll.dat
 EXE:\SYS\PSList.exe /accepteula -x --exestdout=&acn\SYS\PSList.dat
 SAY:
 SAY: 11. Gathering Connection Information...
 SAY:
 EXE:\SYS\cports.exe /scomma &Acq\CPorts.csv
 SAY:
 SAY: 12. Gathering IP Config Information...
 SAY:
 SYS:IPConfig /all --exestdout=&acn\SYS\IPConfig.dat
 SYS:IPConfig /DisplayDNS --exestdout=&acn\SYS\IPCfgDNS.dat
 SAY:
 SAY: 13. Gathering ARP Information...
 SAY:
 SYS:arp -a --exestdout=&acn\SYS\ArpInfo.dat
 SAY:
 SAY: 14. Gathering NetBIOS Information...
 SAY:
 CKY:&Win\System32\NBTStat.exe
  SYS:&Win\System32\NBTStat.exe -scn --exestdout=&acn\SYS\NetBios.dat
 END:
 CKY:&Win\sysnative\NBTStat.exe
  SYS:&Win\sysnative\NBTStat.exe -scn --exestdout=&acn\SYS\NetBios-2.dat
 END:
 SAY:
 SAY: 15. Gathering Open Files/Process Information...
 SAY:
 EXE:\SYS\Handle.exe /accepteula --exestdout=&acn\SYS\OpenFiles.dat
 SAY:
 SAY: 16. Gathering Scheduled Task Information...
 SAY:
 SYS:At --exestdout=&acn\SYS\SchedTasks.dat
 SYS:Schtasks /query /fo LIST /v --exestdout=&acn\SYS\SchedTasks-2.dat
 SAY:
 SAY: 17. Gathering System Logon Information...
 SAY:
 EXE:\SYS\PSLoggedon.exe /accepteula --exestdout=&acn\SYS\Logon.dat
 SAY:
 SAY: 18. Gathering Services Information...
 SAY:
 SYS:Net Start --exestdout=&acn\SYS\Services.dat
 SYS:sc query type= service state= all --exestdout=&acn\SYS\Services-2.dat
 SYS:Tasklist /SVC --exestdout=&acn\SYS\Services-3.dat
 SAY:
 SAY: 19. Gathering Last Activity Information...
 SAY:
 EXE:\SYS\LastActivityView.exe /scomma &Acq\LastActivity.csv
 SAY:
 SAY: 20. Gathering User Assist Information...
 SAY:
 EXE:\SYS\UserAssistView.exe /scomma &Acq\UserAssist.csv
 SAY:
 SAY: 20a. Copying/Zipping Collected System Data...
 SAY:
 SET:ZipFileName=&Acq\Sys.zip
 SET:ZipFileRoot=Sys
 FOR:&Acq\**\*
 ZIP:&For
 SET:ZipFileName=close
 EQU:&VR0 S3
  SAY:
  SAY: 20b. Uploading System Data to S3...
  S3U:&Acq\Sys.zip \&Acn\Sys
 END:
 EQU:&VR0 SFTP
  SAY:
  SAY: 20b. Uploading System Data to SFTP...
  SFU:&Acq\Sys.zip \&Acn\Sys
 END:
 SAY: 21b. Deleting Local Copy...
 CLN:&Acn\Sys
 SAY:
 SAY: 21. Gathering AutoRun Information...
 SAY:
 ACQ:\Arn
 SAY:
 EXE:\SYS\Autorunsc.exe /accepteula -a * -c -h --exestdout=&acn\Arn\AutoRun.dat
 EXE:\SYS\Autorunsc.exe /accepteula -cvm --exestdout=&acn\Arn\AutoRun.cpy
 LST:&acn\Arn\AutoRun.cpy
 CPY:"&LS8" "&ACQ"
 SAY:
 SAY: 21a. Copying/Zipping Autorun Data...
 SAY:
 SET:ZipFileName=&Acq\Arn.zip
 SET:ZipFileRoot=Arn
 FOR:&Acq\**\*
 ZIP:&For
 SET:ZipFileName=close
 EQU:&VR0 S3
  SAY:
  SAY: 21b. Uploading Autoruns to S3...
  S3U:&Acq\Arn.zip \&Acn\Arn
 END:
 EQU:&VR0 SFTP
  SAY:
  SAY: 21b. Uploading Autoruns to SFTP...
  SFU:&Acq\Arn.zip \&Acn\Arn
 END:
 SAY: 21b. Deleting Local Copy...
 CLN:&Acn\Arn
 SAY:
 SAY: 22. Gathering Current (Open) User Registry...
 SAY:
 ACQ:\Reg
 SYS:Reg Save HKCU &Acq\NTUSER.DAT
 SAY:
 SAY: 22a. Gathering All User Registries...
 SAY:
 FOR:${SYSTEMDRIVE}\Users\*\[Nn][Tt][Uu][Ss][Ee][Rr].[Dd][Aa][Tt]
 ACQ:\Reg\&FO2
 EXE:\TSK\fcat.exe /&FO1/&FO2/&FO3 \\.\${SYSTEMDRIVE} --exestdout=&acn\Reg\&FO2\&FNM
 SAY:
 ACQ:\Reg
 EQU:&VR0 S3
  SAY: 22b. Uploading User Registries to S3...
  S3U:&Acq\**\* \&Acn\Reg
 END:
 EQU:&VR0 SFTP
  SAY: 22b. Uploading User Registries to SFTP...
  SFU:&Acq\**\* \&Acn\Reg
 END:
 SAY: 22c. Deleting Local Copy...
 CLN:&Acn\Reg
 SAY:
 SAY: 23. Now Extracting Browser History...
 SAY:
 ACQ:\Brw
 EXE:\SYS\BrowsingHistoryView.exe /scomma &Acq\BrowseHist.csv
 EXE:\SYS\BrowsingHistoryView.exe /shtml &Acq\BrowseHist.htm
 SAY:
 EQU:&VR0 S3
  SAY: 23a. Uploading Browser History to S3...
  S3U:&Acq\* \&Acn\Brw
 END:
 EQU:&VR0 SFTP
  SAY: 23a. Uploading Browser History to SFTP...
  SFU:&Acq\* \&Acn\Brw
 END:
 SAY: 23b. Deleting Local Copy...
 CLN:&Acn\Brw
 SAY:
 SAY: 24. Gathering $Recycle.Bin entries (Going 10 Levels Deep)...
 SAY:
 DSK:Fixed
 ACQ:\RBin
 CPY:"&Dsk:\$Recycle.Bin\**\*" "&Acq"
 SAY:
 EQU:&VR0 S3
  SAY: 24a. Uploading Recycle Bin(s) to S3...
  S3U:&Acq\**\* \&Acn\RBin
 END:
 EQU:&VR0 SFTP
  SAY: 24a. Uploading Recycle Bin(s) to SFTP...
  SFU:&Acq\**\* \&Acn\RBin
 END:
 SAY: 24b. Deleting Local Copy...
 CLN:&Acn\RBin
 SAY:
 SAY: 25. Copying all User PowerShell Logs...
 SAY:
 ACQ:\Psh
 CPY:"${SYSTEMDRIVE}\Users\*\AppData\Roaming\Microsoft\Windows\PowerShell\PSReadLine\*" "&Acq"
 SAY: 25a. Copying/Zipping Powershell Logs...
 SAY:
 SET:ZipFileName=&Acq\Psh.zip
 SET:ZipFileRoot=Psh
 FOR:&Acq\**\*
 ZIP:&For
 SET:ZipFileName=close
 EQU:&VR0 S3
  SAY:
  SAY: 25b. Uploading PowerShell Logs to S3...
  S3U:&Acq\Psh.zip \&Acn\Psh
 END:
 EQU:&VR0 SFTP
  SAY:
  SAY: 25b. Uploading PowerShell Logs to SFTP...
  SFU:&Acq\Psh.zip \&Acn\Psh
 END:
 SAY: 25b. Deleting Local Copy...
 CLN:&Acn\Psh
 SAY:
 SAY: 26. Copying all User Recent LNK files...
 SAY:
 ACQ:\Lnk
 CPY:"${SYSTEMDRIVE}\Users\*\Desktop\*.lnk" "&Acq"
 CPY:"${SYSTEMDRIVE}\Users\*\AppData\Roaming\Microsoft\Windows\Recent\*.lnk" "&Acq"
 SAY: 26a. Copying/Zipping LNK Files...
 SAY:
 SET:ZipFileName=&Acq\Lnk.zip
 SET:ZipFileRoot=Lnk
 FOR:&Acq\**\*
 ZIP:&For
 SET:ZipFileName=close
 EQU:&VR0 S3
  SAY:
  SAY: 26b. Uploading LNK Files to S3...
  S3U:&Acq\Lnk.zip \&Acn\Lnk
 END:
 EQU:&VR0 SFTP
  SAY:
  SAY: 26b. Uploading LNK Files to SFTP...
  SFU:&Acq\Lnk.zip \&Acn\Lnk
 END:
 SAY: 26b. Deleting Local Copy...
 CLN:&Acn\Lnk
 SAY:
 SAY: Acquisition Complete...
 SAY:
 SAY: Now Hashing Artifacts...
 HSH:ACQ
 SAY:
 SAY: 27a. Zipping Final Collection Files ...
 ACQ:\
 SET:ZipFileName=&Acq\Final.zip
 SET:ZipFileRoot=
 FOR:&Acq\*
 ZIP:&For
 SET:ZipFileRoot=Ranprog
 FOR:&Acq\Ranprog\*
 ZIP:&For
 SET:ZipFileName=close
 EQU:&VR0 S3
  SAY: 27b. Final Uploading to S3...
  S3U:&Dir\Logs\&Acn.log \&Acn
  S3U:&Acq\Final.zip \&Acn
 END:
 EQU:&VR0 SFTP
  SAY: 27b. Final Uploading to SFTP...
  SFU:&Dir\Logs\&Acn.log \&Acn
  SFU:&Acq\Final.zip \&Acn
 END:
 SAY: 27c. Deleting All Remaining Files...
 CLN:&Acn
 BYE:
END:
Ver:Linux
 SAY:Linux Collection in Progress...
 SAY:
 SAY: 3. Gathering Disk Information...
 SAY:
 ACQ:/Disk
 SYS:mount --exestdout=&acn/Disk/MountedDisks.dat
 SYS:df -h --exestdout=&acn/Disk/DiskUsage.dat
 SAY:
 SAY: 4. Gathering System Information...
 SAY:
 ACQ:/Sys
 SYS:uname -a --exestdout=&acn/Sys/Uname.dat
 SYS:cat /proc/version --exestdout=&acn/Sys/Version.dat
 SAY:
 SAY: 5. Gathering IPTables Firewall Rules...
 SYS:iptables -L --exestdout=&acn/Sys/IPTables.dat
 SAY: 6. Copying var/log Directory...
 SAY:
 ACQ:/Var/Log
 cpy:/var/log/**/* &Acq
 SAY: 7. Copying Etc Directory...
 SAY:
 ACQ:/Sys/Etc
 CPY:/etc/* &acq
 SAY:
 SAY: 8. Parsing Environment Variables...
 SAY:
 ACQ:/Sys
 SYS:printenv --exestdout=&acn/Sys/EnVar.dat
 SAY:
 SAY: 9. Querying Sofware/Patch List...
 SAY:
 ACQ:/Sys
 SYS:dpkg -l --exestdout=&acn/Sys/dpkg-l.dat
 SYS:rpm -qa --exestdout=&acn/Sys/rpm-qa.dat
 SAY:
 SAY: 10. Gathering Running Process List Information...
 SAY:
 ACQ:/Sys
 SYS:ps aux --forest --exestdout=&acn/Sys/PSList.dat
 SYS:pstree -ah --exestdout=&acn/Sys/PSTree.dat
 SYS:top -b-n 1 --exestdout=&acn/Sys/PSTop.dat
 SYS:sh -c "find /proc/[0-9]*/cmdline | xargs head" --exestdout=&acn/Sys/proc-CommandLines.dat
 SYS:sh -c "find /proc/[0-9]*/exe -print0 | xargs -0 ls -lh" --exestdout=&acn/Sys/proc-Executables.dat
 SYS:sh -c "find /proc/[0-9]*/fd -print0 | xargs -0 ls -lh" --exestdout=&acn/Sys/proc-FDLinks.dat
 SYS:sh -c "find -L /proc/[0-9]*/exe -print0 | xargs -0 sha1sum" --exestdout=&acn/Sys/proc-Hashes.dat
 SAY:
 SAY: 11. Gathering Connection Information...
 SAY:
 ACQ:/Sys
 SYS:netstat -anp --exestdout=&acn/Sys/Netstat-anp.dat
 SYS:netstat -rn --exestdout=&acn/Sys/Netstat-rn.dat
 SYS lsof -i -n -P --exestdout=&acn/Sys/lsofConnections.dat
 SAY:
 SAY: 12. Gathering IP Config Information...
 SAY:
 SYS:ifconfig -a --exestdout=&acn/Sys/IFConfig.dat
 SYS:ip link --exestdout=&acn/Sys/IPLink.dat
 SYS:ip address --exestdout=&acn/Sys/IPAddress.dat
 SYS:ip route --exestdout=&acn/Sys/IPRoute.dat
 SAY:
 SAY: 13. Gathering ARP Information...
 SAY:
 SYS:arp -an --exestdout=&acn/Sys/ArpInfo.dat
 SAY:
 SAY:
 SAY: 15. Gathering Open Files/Process Information...
 SAY:
 ACQ:/Sys
 SYS:lsof --exestdout=&acn/Sys/OpenFiles.dat
 SAY:
 SAY: 16. Gathering Scheduled Task (cron) Information...
 SAY:
 ACQ:/Cron
 cpy:/etc/crontab &Acq
 ACQ:/Cron/D
 cpy:/etc/cron.d/**/* &Acq
 ACQ:/Cron/Hourly
 cpy:/etc/cron.hourly/**/* &Acq
 ACQ:/Cron/Daily
 cpy:/etc/cron.daily/**/* &Acq
 ACQ:/Cron/Weekly
 cpy:/etc/cron.weekly/**/* &Acq
 ACQ:/Cron/Monthly
 cpy:/etc/cron.monthly/**/* &Acq
 SAY:
 SAY: 17. Gathering System Logon Information...
 SAY:
 ACQ:/Sys
 SYS:who --exestdout=&acn/Sys/Logon.dat
 SYS:last --exestdout=&acn/Sys/Last.dat
 SYS:lastb --exestdout=&acn/Sys/LastB.dat
 SYS:lastlog --exestdout=&acn/Sys/LastLog.dat
 SAY:
 SAY:
 SAY: 18. Gathering Services Information...
 SAY:
 ACQ:/Sys
 SYS:service --status-all --exestdout=&acn/Sys/Services.dat
 SAY: 19. Gathering Persistence Information...
 SYS:sh -c "find /etc/systemd/ -name '*.service' -print0 | xargs -0 cat" --exestdout=&acn/Sys/SystemD.dat
 SYS:systemctl list-unit-files --exestdout=&acn/Sys/SystemCTL.dat
 SAY:
 SAY: 20. Gathering User Information...
 SAY:
 ACQ:/UserInfo
 SYS:whoami --exestdout=&acn/UserInfo/Whoami.dat
 SYS:logname --exestdout=&acn/UserInfo/Logname.dat
 SAY:
 SAY: 22. Gathering Current User Config Files...
 SAY:
 ACQ:/Home
 cpy:/home/**/.* &Acq
 SAY:Enumerating All Files...
 ACQ:/Disk
 SYS: find / -printf "%i, %d, %b, %A+ %AZ, %C+ %CZ, %T+ %TZ, %M, %u, %g, %p\n" --exestdout=&Acn/Disk/AllFiles.csv
 HSH:ACQ
 SAY:
 SAY: 23. Zipping Evidence for Upload...
 ACQ:/
 FOR:&Acq/**/*
 ACQ:/Zip/Split
 ACQ:/Zip
 SET:ZipFileName=&Acq/&Acn.zip
 SET:ZipFileRoot=AChoirX
 ZIP:&For
 SET:ZipFileName=close
 SAY:
 SAY: 23a. Splitting Evidence for Upload...
 SYS:split -b 900m "&Acq/&Acn.zip" "&Acq/Split/&Acn.zip-"
 EQU:&VR0 S3
  SAY:
  SAY: 23b. Uploading Evidence to S3...
  S3U:&Acq/Split/* /&Acn
  S3U:&Dir/Logs/&Acn.log /&Acn
 END:
 EQU:&VR0 SFTP
  SAY:
  SAY: 23b. Uploading Evidence to SFTP...
  SFU:&Acq/Split/* /&Acn
  SFU:&Dir/Logs/&Acn.log /&Acn
 END:
 SAY: 23c. Deleting Local Copy...
 CLN:&Acn
 SAY:
 BYE:
END:
Ver:OSX
 SAY:OSX Acquisition Not Available Yet (Coming Soon)...
 SAY:
 *PZZ: All Done!  Press Any Key to Finish.
 BYE:
END:
