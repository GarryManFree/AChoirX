*******************************************************
* AChoirX YARA Process Memory Scanner                 *
*  /VR0:<Process Image Name>                          *
*                                                     *
*  Note1: Ommitting /VR0 will scan all Processes      *
*  Note2: /VR0 accepts WildCards                      *
*******************************************************
SET:CopyPath=Part
CON:MSGLevel=min
*******************************************************
* Are we looking at specific Processes?               *
*******************************************************
EQU:.&VR0 .
 SAY:[+] Collecting FULL Processes List from Memory...
 ACQ:\YARA
 SYS:TaskList /NH /FO CSV --exestdout=&Acn\YARA\TaskList.txt
END:
*******************************************************
* Run Tasklist to get Processes and their PIDs        *
*******************************************************
NEQ:.&VR0 .
 SAY:[+] Collecting &VR0 Process List from Memory...
 ACQ:\YARA
 SYS:TaskList /FI "IMAGENAME eq &VR0" /NH /FO CSV --exestdout=&Acn\YARA\TaskList.txt
END:
*******************************************************
* Run YARA Against the list of PIDs                   *
*******************************************************
SAY:[+] Scanning Processes...
LST:&Acn\YARA\TaskList.txt
EXE:\YARA\yara64.exe &dir\YARA\test.yar &LS1 --exestdout=&Acn\YARA\&Num.YYY
SAY:[+] Total Processes Scanned: &CNR
*******************************************************
* Loop to Format it into useful output                *
*******************************************************
SAY:[+] Process: &LS0  PID:&LS1
SAY:[+] YARA Scan Results...
CN0:&CNR
CN1:0
SET:Delims= \/
LBL:PIDLoop
N==:&CN1 &CN0
 JMP:PIDExit
END:
N>>:&CN1 &CN0
 JMP:PIDExit
END:
CN1:++
LST:&Acn\YARA\&CN1.YYY
SAY:[+] YARA Match Found: &LS0 in ProcessID: &LS1
JMP:PIDLoop
LBL:PIDExit
