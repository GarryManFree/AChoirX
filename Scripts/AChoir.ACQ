*******************************************************
* Live Acquisition Script (2.0)                       *
* - Pulls Forensic artifacts & telemetry for analysis *
* - Created by D0n Quix0te/OMENScan (May 2015)        *
* - Inspired by many other tools                      *
* - Recognize:Windows, Linux, or OSX                  *
*******************************************************
SET:CopyPath=Part
SAY:
SAY: AChoir will pull artifacts and telemetry of forensic value for investigation.
SAY:
SAY: * * *   Please MAKE SURE you are running this as an Administrator  * * * 
SAY: * * * Administrator right are required for many of these utilities * * * 
SAY:
PZZ: Press Any Key to Continue, or q to QUIT
SAY:
SAY: 1. Now Dumping Memory...
SAY:
SAY:  WARNING: This will take while, and will create a HUGE memory image File.
SAY:  You've been warned ;-) 
SAY:
VER:Windows
 N<<:&Dsa &Mem
  SAY: Not Enough disk space in &ACQ to Capture Memory: &MEM ... 
  SAY: Bypassing Memory Capture!
 END:
 N>>:&Dsa &Mem
  ACQ:\MemDump
  EXE:\MEM\winpmem_3.3rc3.exe --output &Acq\WinPmemDump.Raw --logfile &Acq\WinPmemDump.Log --volume_format raw -dd -t
  ***:Use AFF format with --split for creating smaller files for more reliable Transfer if needed
  ***:EXE:\MEM\WinPmem_3.3rc3.exe --output &Acq\WinPmemDump.Aff --split 2000m --logfile &Acq\WinPmemDump.Log -dd -t
  END:
 SAY:
 SAY: 2. Extracting Prefetch Directory...
 SAY:
 ACQ:\Prf
 CPY:"&Win\prefetch\**\*" "&Acq"
 SAY:
 SAY: 3a. Copy Raw $MFT(s)...
 SAY:
 DSK:Fixed
 ACQ:\RawData
 EXE:\TSK\sleuthkit-4.10.0-win32\bin\fcat.exe -f ntfs $MFT \\.\&dsk: --exestdout=&acn\RawData\MFT-&Dsk
 SAY:
 SAY: 3b. Copy Raw $LogFile...
 SAY:
 EXE:\TSK\sleuthkit-4.10.0-win32\bin\fcat.exe -f ntfs $LogFile \\.\&dsk: --exestdout=&acn\RawData\LogFile-&Dsk
 SAY:
 SAY: 3c. Now Extracting USNJrnl...
 SAY:
 EXE:\DSK\ExtractUSNJrnl.exe /DevicePath:c: /OutputPath:&Acq
 SAY:
 SAY: 3d. Now Parsing USNJrnl...
 SAY:
 SYS:fsutil usn enumdata 1 0 1 C: --exestdout=&acn\RawData\USNJrnl.dat
 SYS:fsutil usn readjournal C: csv --exestdout=&acn\RawData\USNJrnl.csv
 SAY:
 SAY: 4a. Gathering System Information...
 SAY:
 ACQ:\
 EXE:\SYS\PSInfo.exe /accepteula -s --exestdout=&acn\Info.dat
 SAY:
 SAY: 4b. Gathering System Audit Information...
 SAY:
 EXE:\SYS\WinAudit.exe /r=gsoPxuTUeERNtnzDaIbMpmidcSArCOHG /f=&Acq\WinAudit.htm /l=&Acq\WinAudLog.txt
 SAY:
 SAY: 4c. Gathering Group Policy Information...
 SAY:
 SYS:GPResult /R /Z --exestdout=&acn\GPResult.txt
 SAY:
 SAY: 5. Raw Copy Registry Hives...
 SAY:
 ACQ:\Reg
 EXE:\TSK\sleuthkit-4.10.0-win32\bin\fcat.exe /Windows/System32/Config/SECURITY \\.\${SYSTEMDRIVE} --exestdout=&acn\Reg\SECURITY
 EXE:\TSK\sleuthkit-4.10.0-win32\bin\fcat.exe /Windows/System32/Config/SOFTWARE \\.\${SYSTEMDRIVE} --exestdout=&acn\Reg\SOFTWARE
 EXE:\TSK\sleuthkit-4.10.0-win32\bin\fcat.exe /Windows/System32/Config/SAM \\.\${SYSTEMDRIVE} --exestdout=&acn\Reg\SAM
 EXE:\TSK\sleuthkit-4.10.0-win32\bin\fcat.exe /Windows/System32/Config/SYSTEM \\.\${SYSTEMDRIVE} --exestdout=&acn\Reg\SYSTEM
 CPY:"&Win\AppCompat\Programs\Amcache.hve" "&Acq"
 SAY:
 SAY: 6. Copying (System32) EventViewer Logs...
 SAY:
 ACQ:\Evt
 ACQ:\Evt\Sys32
 CPY:"&Win\System32\winevt\Logs\*" "&Acq"
 SAY:
 SAY: 6a. Copying (Sysnative) EventViewer Logs...
 SAY:
 ACQ:\Evt\Nativ
 CPY:"&Win\sysnative\winevt\Logs\*" "&Acq"
 SAY:
 SAY: 7. Parsing (System32) Etc Directory, and SRUM...
 SAY:
 ACQ:\SYS
 ACQ:\SYS\Sys32
 CPY:"&Win\System32\Drivers\Etc\*" "&Acq"
 CPY:"&Win\System32\sru\SRUDB.dat" "&Acq"
 SAY:
 SAY: 7a. Parsing (Sysnative) Hosts And Services Directory...
 SAY:
 ACQ:\SYS\Nativ
 CPY:"&Win\sysnative\Drivers\Etc\*" "&Acq"
 CPY:"&Win\sysnative\sru\SRUDB.dat" "&Acq"
 SAY:
 SAY: 8. Parsing Environment Variables...
 SAY:
 ACQ:\SYS
 SYS:CMD /c Set --exestdout=&acn\SYS\EnVar.dat
 SAY:
 SAY: 9. Parsing The Patch List...
 SAY:
 SYS:WMIC qfe list --exestdout=&acn\SYS\QFEList.dat
 SAY:
 SAY: 10. Gathering Running Process List Information...
 SAY:
 SYS:Tasklist /v --exestdout=&acn\SYS\Tasklist.dat
 SYS:Tasklist /M --exestdout=&acn\SYS\TaskAll.dat
 EXE:\SYS\PSList.exe /accepteula -x --exestdout=&acn\SYS\PSList.dat
 SAY:
 SAY: 11. Gathering Connection Information...
 SAY:
 EXE:\SYS\cports.exe /scomma &Acq\CPorts.csv
 SAY:
 SAY: 12. Gathering IP Config Information...
 SAY:
 SYS:IPConfig /all --exestdout=&acn\SYS\IPConfig.dat
 SYS:IPConfig /DisplayDNS --exestdout=&acn\SYS\IPCfgDNS.dat
 SAY:
 SAY: 13. Gathering ARP Information...
 SAY:
 SYS:arp -a --exestdout=&acn\SYS\ArpInfo.dat
 SAY:
 SAY: 14. Gathering NetBIOS Information...
 SAY:
 CKY:&Win\System32\NBTStat.exe
  SYS:&Win\System32\NBTStat.exe -scn --exestdout=&acn\SYS\NetBios.dat
 END:
 CKY:&Win\sysnative\NBTStat.exe
  SYS:&Win\sysnative\NBTStat.exe -scn --exestdout=&acn\SYS\NetBios-2.dat
 END:
 SAY:
 SAY: 15. Gathering Open Files/Process Information...
 SAY:
 EXE:\SYS\Handle.exe /accepteula --exestdout=&acn\SYS\OpenFiles.dat
 SAY:
 SAY: 16. Gathering Scheduled Task Information...
 SAY:
 SYS:At --exestdout=&acn\SYS\SchedTasks.dat
 SYS:Schtasks /query /fo LIST /v --exestdout=&acn\SYS\SchedTasks-2.dat
 SAY:
 SAY: 17. Gathering System Logon Information...
 SAY:
 EXE:\SYS\PSLoggedon.exe /accepteula --exestdout=&acn\SYS\Logon.dat
 SAY:
 SAY: 18. Gathering Services Information...
 SAY:
 SYS:Net Start --exestdout=&acn\SYS\Services.dat
 SYS:sc query type= service state= all --exestdout=&acn\SYS\Services-2.dat
 SYS:Tasklist /SVC --exestdout=&acn\SYS\Services-3.dat
 SAY:
 SAY: 19. Gathering Last Activity Information...
 SAY:
 EXE:\SYS\LastActivityView.exe /scomma &Acq\LastActivity.csv
 SAY:
 SAY: 20. Gathering User Assist Information...
 SAY:
 EXE:\SYS\UserAssistView.exe /scomma &Acq\UserAssist.csv
 SAY:
 SAY: 21. Gathering AutoRun Information...
 SAY:
 ACQ:\Arn
 SAY:
 EXE:\SYS\Autorunsc.exe /accepteula -a * -c -h --exestdout=&acn\Arn\AutoRun.dat
 EXE:\SYS\Autorunsc.exe /accepteula -cvm --exestdout=&acn\Arn\AutoRun.cpy
 LST:&acn\Arn\AutoRun.cpy
 CPY:"&LS8" "&ACQ"
 SAY:
 SAY: 22a. Gathering Current (Open) User Registry...
 SAY:
 ACQ:\Reg
 SYS:Reg Save HKCU &Acq\NTUSER.DAT
 SAY:
 SAY: 22b. Gathering All User Registries...
 SAY:
 FOR:C:\Users\*\ntuser.dat
 ACQ:\Reg\&FO2
 EXE:\TSK\sleuthkit-4.10.0-win32\bin\fcat.exe /&FO1/&FO2/&FO3 \\.\${SYSTEMDRIVE} --exestdout=&acn\Reg\&FO2\&FNM
 SAY:
 SAY: 23. Now Extracting Browser History...
 SAY:
 ACQ:\Brw
 EXE:\SYS\BrowsingHistoryView.exe /scomma &Acq\BrowseHist.csv
 EXE:\SYS\BrowsingHistoryView.exe /shtml &Acq\BrowseHist.htm
 SAY:
 SAY: 24. Gathering $Recycle.Bin entries (Going 10 Levels Deep)...
 SAY:
 DSK:Fixed
 ACQ:\RBin
 CPY:"&Dsk:\$Recycle.Bin\**\*" "&Acq"
 SAY:
 SAY: Acquisition Complete...
 SAY:
 SAY: Now Hashing Artifacts...
 HSH:ACQ
 SAY:
 PZZ: All Done!  Press Any Key to Finish.
 BYE:
END:
Ver:Linux
 SAY:Linux Acquisition (A Work In Progress)...
 SAY:
 SAY: 3. Gathering Disk Information...
 SAY:
 ACQ:/Disk
 SYS:mount --exestdout=&acn/Disk/MountedDisks.dat
 SYS:df -h --exestdout=&acn/Disk/DiskUsage.dat
 SAY:
 SAY: 4. Gathering System Information...
 SAY:
 ACQ:/Sys
 SYS:uname -a --exestdout=&acn/Sys/Uname.dat
 SYS:cat /proc/version --exestdout=&acn/Sys/Version.dat
 SAY:
 SAY: 6. Copying var/log Directory...
 SAY:
 ACQ:/Var/Log
 cpy:/var/log/**/* &Acq
 SAY: 7. Copying Etc Directory...
 SAY:
 ACQ:/Sys/Etc
 CPY:/etc/* &acq
 SAY:
 SAY: 8. Parsing Environment Variables...
 SAY:
 ACQ:/Sys
 SYS:printenv --exestdout=&acn/Sys/EnVar.dat
 SAY:
 SAY: 9. Querying Sofware/Patch List...
 SAY:
 ACQ:/Sys
 SYS:dpkg -l --exestdout=&acn/Sys/dpkg-l.dat
 SYS:rpm -qa --exestdout=&acn/Sys/rpm-qa.dat
 SAY:
 SAY: 10. Gathering Running Process List Information...
 SAY:
 ACQ:/Sys
 SYS:ps aux --forest --exestdout=&acn/Sys/PSList.dat
 SYS:pstree -ah --exestdout=&acn/Sys/PSTree.dat
 SYS:top -b-n 1 --exestdout=&acn/Sys/PSTop.dat
 SAY:
 SAY: 11. Gathering Connection Information...
 SAY:
 ACQ:/Sys
 SYS:netstat -anp --exestdout=&acn/Sys/Netstat-anp.dat
 SYS:netstat -rn --exestdout=&acn/Sys/Netstat-rn.dat
 SYS lsof -i -n -P --exestdout=&acn/Sys/lsofConnections.dat
 SAY:
 SAY: 12. Gathering IP Config Information...
 SAY:
 SYS:ifconfig -a --exestdout=&acn/Sys/IFConfig.dat
 SYS:ip link --exestdout=&acn/Sys/IPLink.dat
 SYS:ip address --exestdout=&acn/Sys/IPAddress.dat
 SYS:ip route --exestdout=&acn/Sys/IPRoute.dat
 SAY:
 SAY: 13. Gathering ARP Information...
 SAY:
 SYS:arp -an --exestdout=&acn/Sys/ArpInfo.dat
 SAY:
 SAY:
 SAY: 15. Gathering Open Files/Process Information...
 SAY:
 ACQ:/Sys
 SYS:lsof --exestdout=&acn/Sys/OpenFiles.dat
 SAY:
 SAY: 16. Gathering Scheduled Task (cron) Information...
 SAY:
 ACQ:/Cron
 cpy:/etc/crontab &Acq
 ACQ:/Cron/D
 cpy:/etc/cron.d/**/* &Acq
 ACQ:/Cron/Hourly
 cpy:/etc/cron.hourly/**/* &Acq
 ACQ:/Cron/Daily
 cpy:/etc/cron.daily/**/* &Acq
 ACQ:/Cron/Weekly
 cpy:/etc/cron.weekly/**/* &Acq
 ACQ:/Cron/Monthly
 cpy:/etc/cron.monthly/**/* &Acq
 SAY:
 SAY: 17. Gathering System Logon Information...
 SAY:
 ACQ:/Sys
 SYS:who --exestdout=&acn/Sys/Logon.dat
 SYS:last --exestdout=&acn/Sys/Last.dat
 SYS:lastb --exestdout=&acn/Sys/LastB.dat
 SYS:lastlog --exestdout=&acn/Sys/LastLog.dat
 SAY:
 SAY:
 SAY: 18. Gathering Services Information...
 SAY:
 ACQ:/Sys
 SYS:service --status-all --exestdout=&acn/Sys/Services.dat
 SAY:
 SAY: 20. Gathering User Information...
 SAY:
 ACQ:/UserInfo
 SYS:whoami --exestdout=&acn/UserInfo/Whoami.dat
 SYS:logname --exestdout=&acn/UserInfo/Logname.dat
 SAY:
 SAY: 22. Gathering Current User Config Files...
 SAY:
 ACQ:/Home
 cpy:/home/**/.* &Acq
 HSH:ACQ
 SAY:
 PZZ: All Done!  Press Any Key to Finish.
 BYE:
END:
Ver:OSX
 SAY:OSX Acquisition (A Work In Progress)...
 SAY:
 SAY: 3. Gathering Disk Information...
 SAY:
 ACQ:/Disk
 SYS:mount --exestdout=&acn/Disk/MountedDisks.dat
 SYS:df -h --exestdout=&acn/Disk/DiskUsage.dat
 SAY:
 SAY: 4. Gathering System Information...
 SAY:
 ACQ:/Sys
 SYS:system_profiler --exestdout=&acn/Sys/Profiler.dat
 SAY:
 SAY: 6. Copying var/log Directory...
 SAY:
 ACQ:/Var/Log
 CPY:/var/log/**/* &Acq
 ACQ:/Var/Log/Private
 CPY:/private/var/log/**/* &Acq
 SAY: 7. Copying Etc Directory...
 SAY:
 ACQ:/Sys/Etc
 CPY:/etc/* &acq
 SAY:
 SAY: 8. Parsing Environment Variables...
 SAY:
 ACQ:/Sys
 SYS:printenv --exestdout=&acn/Sys/EnVar.dat
 SAY:
 SAY: 9. Querying Sofware/Patch List...
 SAY:
 ACQ:/Sys
 SYS:pkgutil --pkgs --exestdout=&acn/Sys/pkgutil-pkgs.dat
 SAY:
 SAY: 10. Gathering Running Process List Information...
 SAY:
 ACQ:/Sys
 SYS:ps aux --forest --exestdout=&acn/Sys/PSList.dat
 SYS:ps -ef --exestdout=&acn/Sys/PS-ef.dat
 SYS:top -l 1 --exestdout=&acn/Sys/PSTop.dat
 SAY:
 SAY: 11. Gathering Connection Information...
 SAY:
 ACQ:/Sys
 SYS:netstat -anp --exestdout=&acn/Sys/Netstat-anp.dat
 SYS:netstat -rn --exestdout=&acn/Sys/Netstat-rn.dat
 SYS lsof -i -n -P --exestdout=&acn/Sys/lsofConnections.dat
 SAY:
 SAY: 12. Gathering IP Config Information...
 SAY:
 SYS:ifconfig -a --exestdout=&acn/Sys/IFConfig.dat
 SAY:
 SAY: 13. Gathering ARP Information...
 SAY:
 SYS:arp -an --exestdout=&acn/Sys/ArpInfo.dat
 SAY:
 SAY:
 SAY: 15. Gathering Open Files/Process Information...
 SAY:
 ACQ:/Sys
 SYS:lsof --exestdout=&acn/Sys/OpenFiles.dat
 SAY:
 SAY: 16. Gathering Scheduled Task (cron) Information...
 SAY:
 ACQ:/Cron
 cpy:/etc/crontab &Acq
 ACQ:/Cron/Usr
 cpy:/usr/lib/cron/tabs/**/*
 SAY:
 SAY: 17. Gathering System Logon Information...
 SAY:
 ACQ:/Sys
 SYS:who --exestdout=&acn/Sys/Logon.dat
 SYS:last --exestdout=&acn/Sys/Last.dat
 SAY:
 SAY:
 SAY: 18. Gathering Services Information...
 SAY:
 ACQ:/Sys
 SYS:launchctl list --exestdout=&acn/Sys/Services.dat
 SAY:
 SAY: 20. Gathering User Information...
 SAY:
 ACQ:/UserInfo
 SYS:whoami --exestdout=&acn/UserInfo/Whoami.dat
 SYS:logname --exestdout=&acn/UserInfo/Logname.dat
 SAY:
 SAY: 22. Gathering Current User Config Files...
 SAY:
 ACQ:/Home
 cpy:/users/**/.* &Acq
 HSH:ACQ
 SAY:
 BYE:
END:
