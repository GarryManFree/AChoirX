*******************************************************
* AChoirX and Plaso Timeline an AChoirX collection    *
* - Win Requires Plaso to be in &Dir\Plaso directory  *
* - Lin Requires Plaso to be in /usr/bin directory    *
* - OSX not yet implemented                           *
*   - AChoirX will Check                              *
* - Recognize:Windows, Linux, or OSX                  *
*******************************************************
SET:CopyPath=Part
****************************************************************
* Windows: Is Plaso in the AChoir directory? If not, Install   *
****************************************************************
VER:Windows
  SAY:[+] Windows Timelining...
  SAY:[+] Checking for Plaso in: &Dir\Plaso
  CKN:&Dir\Plaso\log2timeline.exe
    DIR:\Plaso
    SAY:
    SAY:[*] Log2TimeLine Not Found, Downloading it... 
    SAY:
    GET:https://github.com/OMENScan/AChoir/blob/master/Tools/plaso-20191203-py3.7-amd64.zip?raw=true &Dir\Plaso.zip
    SAY:
    SAY:[*] Unzipping Plaso...
    SAY:
    UNZ:&Dir\Plaso.zip &Dir
    SAY:
  END:
  DIR:\
  REQ:&Dir\Plaso\log2timeline.exe
  REQ:&Dir\Plaso\psort.exe
  SAY:
  SAY:[+] Plaso (log2timeline, psort) has been found in: &Dir\Plaso\
  *******************************************************
  * Get the Collection To Timeline                      *
  *******************************************************
  ACQ:\TimeLine
  SAY:
  LBL:ChkWinDir
  VR0:GotWinDir
  INP:Enter the Collection Directory to TimeLine: 
  CKN:&Dir\&Inp
    SAY:
    SAY:[!] That Collection Does Not Exist.
    VR0:ChkWinDir
  END:
  JMP:&VR0
  LBL:GotWinDir
  SAY:
  SAY:[+] Now TimeLining &Dir\&Inp
  SAY:
  *******************************************************
  * Timeline MFT(s)                                     *
  *******************************************************
  SAY:[+] Now TimeLining &Dir\&Inp\RawData\MFT*
  SAY:
  FOR:&Dir\&Inp\RawData\MFT*
  EXE:&Dir\Plaso\Log2TimeLine.exe -z UTC --parsers "mft" --status_view none --quiet --logfile "&Acq\Temp.Log" &Acq\timeline.bodyfile "&For"
  *******************************************************
  * Timeline Event ViewerLogs                           *
  *******************************************************
  SAY:
  SAY:[+] Now TimeLining &Dir\&Inp\Evt\**\*.evtx
  SAY:
  FOR:&Dir\&Inp\Evt\**\*.evtx
  EXE:&Dir\Plaso\Log2TimeLine.exe -z UTC --parsers "winevt,winevtx" --status_view none --quiet --logfile "&Acq\Temp.Log" &Acq\timeline.bodyfile "&For"
  *******************************************************
  * Timeline Prefetch                                   *
  *******************************************************
  SAY:
  SAY:[+] Now TimeLining &Dir\&Inp\Prf\*.*
  SAY:
  FOR:&Dir\&Inp\Prf\*.*
  EXE:&Dir\Plaso\Log2TimeLine.exe -z UTC --parsers "prefetch" --status_view none --quiet --logfile "&Acq\Temp.Log" &Acq\timeline.bodyfile "&For"
  *******************************************************
  * Timeline Registrties                                *
  *******************************************************
  SAY:
  SAY:[+] Now TimeLining &Dir\&Inp\Reg\**\*
  SAY:
  FOR:&Dir\&Inp\Reg\**\*
  EXE:&Dir\Plaso\Log2TimeLine.exe -z UTC --parsers "winreg_default,winreg" --status_view none --quiet --logfile "&Acq\Temp.Log" &Acq\timeline.bodyfile "&For"
  SAY:
  SAY:[+] Timeline Generation Completed...
  SAY:
  *******************************************************
  * Convert BodyFile to CSV                             *
  *******************************************************
  LBL:BodyWinCSV
  VR0:BodyWinCSV
  INP:Would you like to convert the BodyFile to CSV (y/n)?
  EQU:&Inp y
    VR0:AllWinDun
    SAY:
    SAY:[+] Coverting BodyFile to CSV
    EXE:&Dir\Plaso\psort.exe -z UTC -o l2tcsv -w &Acq\timeline.csv &Acq\timeline.bodyfile
  END:
  EQU:&Inp n
    VR0:AllWinDun
    SAY:
    SAY:[+] Bypass Coverting BodyFile to CSV
  END:
  JMP:&VR0
  LBL:AllWinDun
END:
****************************************************************
* Linux: Is Plaso in the AChoir directory? If not, Install   *
****************************************************************
VER:Linux
  SAY:[+] Linux Timelining...
  SAY:[+] Checking for Plaso in: /usr/bin
  CKN:/usr/bin/log2timeline.py
    SAY:
    SAY: * * *  Plaso was not found on this machine.  I can install it if you want.  * * * 
    SAY: * * *    PLEASE ENTER "Q" to QUIT if YOU DO NOT WANT ME TO INSTALL PLASO    * * * 
    SAY:
    PZZ:[?] Press Any Key to Continue Installing Plaso, or q to QUIT
    SYS:sudo apt-get install plaso
    SAY:
  END:
  DIR:/
  REQ:/usr/bin/log2timeline.py
  REQ:/usr/bin/psort.py
  SAY:
  SAY:[+] Plaso (log2timeline, psort) has been found in: /usr/bin
  *******************************************************
  * Get the Collection To Timeline                      *
  *******************************************************
  ACQ:/TimeLine
  SAY:
  LBL:ChkLinDir
  VR0:GotLinDir
  INP:Enter the Collection Directory to TimeLine: 
  CKN:&Dir/&Inp
    SAY:
    SAY:[!] That Collection Does Not Exist.
    VR0:ChkLinDir
  END:
  JMP:&VR0
  LBL:GotLinDir
  SAY:
  SAY:[+] Now TimeLining &Dir/&Inp
  SAY:
  *******************************************************
  * Timeline MFT(s)                                     *
  *******************************************************
  SAY:[+] Now TimeLining &Dir/&Inp/RawData/MFT*
  SAY:
  FOR:&Dir/&Inp/RawData/MFT*
  SYS:python /usr/bin/log2timeline.py -z UTC --parsers "mft" --status_view none --quiet --logfile "&Acq/Temp.Log" &Acq/timeline.bodyfile "&For"
  *******************************************************
  * Timeline Event ViewerLogs                           *
  *******************************************************
  SAY:
  SAY:[+] Now TimeLining &Dir/&Inp/Evt/**/*.evtx
  SAY:
  FOR:&Dir/&Inp/Evt/**/*.evtx
  SYS:python /usr/bin/log2timeline.py -z UTC --parsers "winevt,winevtx" --status_view none --quiet --logfile "&Acq/Temp.Log" &Acq/timeline.bodyfile "&For"
  *******************************************************
  * Timeline Prefetch                                   *
  *******************************************************
  SAY:
  SAY:[+] Now TimeLining &Dir/&Inp/Prf/*.*
  SAY:
  FOR:&Dir/&Inp/Prf/*.*
  SYS:python /usr/bin/log2timeline.py -z UTC --parsers "prefetch" --status_view none --quiet --logfile "&Acq/Temp.Log" &Acq/timeline.bodyfile "&For"
  *******************************************************
  * Timeline Registrties                                *
  *******************************************************
  SAY:
  SAY:[+] Now TimeLining &Dir/&Inp/Reg/**/*
  SAY:
  FOR:&Dir/&Inp/Reg/**/*
  SYS:python /usr/bin/log2timeline.py -z UTC --parsers "winreg_default,winreg" --status_view none --quiet --logfile "&Acq/Temp.Log" &Acq/timeline.bodyfile "&For"
  SAY:
  SAY:[+] Timeline Generation Completed...
  SAY:
  *******************************************************
  * Convert BodyFile to CSV                             *
  *******************************************************
  LBL:BodyLinCSV
  VR0:BodyLinCSV
  INP:Would you like to convert the BodyFile to CSV (y/n)?
  EQU:&Inp y
    VR0:AllLinDun
    SAY:
    SAY:[+] Coverting BodyFile to CSV
    SYS:python /usr/bin/psort.py -z UTC -o l2tcsv -w &Acq/timeline.csv &Acq/timeline.bodyfile
  END:
  EQU:&Inp n
    VR0:AllLinDun
    SAY:
    SAY:[+] Bypass Coverting BodyFile to CSV
  END:
  JMP:&VR0
  LBL:AllLinDun
END:
Ver:OSX
 SAY:OSX Plaso Not Yet Implemented...
END:
****************************************************************
* All Processing Complete - Windows or Linux                   *
****************************************************************
SAY:
SAY:[+] Processing Complete...  Bye...
